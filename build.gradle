plugins {
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
}

allprojects {
    group = 'io.github.tiamatxu.distributedtask'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.18"
            mavenBom "org.apache.dubbo:dubbo-bom:3.2.9"
            mavenBom "org.apache.curator:apache-curator:5.2.1"
        }
        dependencies {
            dependency 'redis.clients:jedis:4.4.3'
            dependency 'org.apache.zookeeper:zookeeper:3.8.0'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }
}

bootJar {
    enabled = false
}

subprojects {
    buildDir = new File(rootProject.buildDir, name)

    apply plugin: 'org.springframework.boot'

    sourceSets {
        main {
            resources {
                srcDirs += files("$rootDir/resources")
            }
        }
        test {
            resources {
                srcDirs += files("$rootDir/resources")
            }
        }
    }

    dependencies {
        // Common dependencies for all sub-modules can be added here
    }

    tasks.withType(JavaExec) {
        systemProperty 'moduleName', project.name
    }
}
